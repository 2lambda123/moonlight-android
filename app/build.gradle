import com.android.builder.model.ProductFlavor
import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.2'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27

        versionName "5.6.2"
        versionCode = 140
    }

    flavorDimensions "root"

    productFlavors {
        root {
            // Android O has native mouse capture, so don't show the rooted
            // version to devices running O on the Play Store.
            maxSdkVersion 25

            externalNativeBuild {
                ndkBuild {
                    arguments "PRODUCT_FLAVOR=root"
                }
            }

            applicationId "com.limelight.root"
            dimension "root"
        }

        nonRoot {
            externalNativeBuild {
                ndkBuild {
                    arguments "PRODUCT_FLAVOR=nonRoot"
                }
            }

            applicationId "com.limelight"
            dimension "root"
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }

    externalNativeBuild {
        ndkBuild {
            path "src/main/jni/Android.mk"
        }
    }
}

dependencies {
    implementation 'org.bouncycastle:bcprov-jdk15on:1.57'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.57'
    implementation files('libs/jcodec-0.1.9-patched.jar')

    implementation project(':moonlight-common')
}
